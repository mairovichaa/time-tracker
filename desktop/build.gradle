plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.12'
}

tasks.register('fatJar', org.gradle.jvm.tasks.Jar) {
    dependsOn 'build'

    archiveBaseName = 'app'

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Main-Class': 'time_tracker.Launcher'
        )
    }
    from sourceSets.main.output
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

dependencies {
    implementation project(':core:repository')

    implementation 'io.github.palexdev:materialfx:11.13.5'

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"

    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:win"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:linux"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:mac"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:mac-aarch64"
}

javafx {
    version = '21-ea+24'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.swing', 'javafx.web']
}


